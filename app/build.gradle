apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
apply plugin: 'com.polidea.cockpit'
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "pl.gov.mc.protego"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode System.env.CI_JOB_ID ? System.env.CI_JOB_ID.toInteger() : 1
        versionName System.env.CI_COMMIT_TAG ? System.env.CI_COMMIT_TAG : "0.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../keystore.jks")
            storePassword System.env.STORE_PASSWORD
            keyAlias System.env.KEY_ALIAS
            keyPassword System.env.KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationIdSuffix ".prod"
            versionNameSuffix "-prod"
            buildConfigField "String", "BASE_API_URL", "\"https://google.com/\""
            buildConfigField "int", "SUPPORTED_API_VERSION", "1"
        }
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            debuggable true
            buildConfigField "String", "BASE_API_URL", "\"https://europe-west3-protego-dev.cloudfunctions.net/\""
            buildConfigField "int", "SUPPORTED_API_VERSION", "1"
        }
        stg {
            initWith debug
            debuggable true
            zipAlignEnabled false
            applicationIdSuffix ".stg"
            versionNameSuffix "-stg"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        lintConfig file("lint.xml")
    }

    testOptions {
        unitTests.returnDefaultValues = true
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }

        }
        unitTests.all {
            testLogging {
                events = ["passed", "skipped", "failed"]
                showStackTraces = true
                exceptionFormat = 'full'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core:1.2.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'

    // Crashlytics for dev build
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0'
    debugImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // Leak Canary for dev build
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'

    // Koin
    implementation 'org.koin:koin-android:2.1.5'
    implementation "org.koin:koin-androidx-viewmodel:2.1.5"

    // Cockpit, the debug menu
    debugImplementation 'com.polidea.cockpit:cockpit:3.1.2'
    debugImplementation 'com.squareup:seismic:1.0.2'
    stgImplementation 'com.polidea.cockpit:cockpit:3.1.2'
    stgImplementation 'com.squareup:seismic:1.0.2'

    // Rx
    implementation "io.reactivex.rxjava2:rxkotlin:2.3.0"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // spek
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:$spek_version"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    //retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.7.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.3.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.2'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
