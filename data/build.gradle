apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply from: '../ktlint.gradle'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath deps.gradle_plugin.wire_compiler
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "SHARED_PREFERENCES_FILE_NAME", SHARED_PREFERENCES_FILE_NAME
        buildConfigField "boolean", "ENABLE_WEBVIEW_LOGS", "false"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    kotlinOptions { jvmTarget = "1.8" }

    buildTypes {
        debug {
            buildConfigField "boolean", "ENABLE_WEBVIEW_LOGS", "true"
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        sauceLab {
            initWith buildTypes.release
        }
    }
    flavorDimensions "mode"
    productFlavors {
        dev {
            dimension "mode"
            buildConfigField "String", "MAIN_TOPIC", "\"main-localized-dev\""
            buildConfigField "String", "DAILY_TOPIC", "\"daily-localized-dev\""
            buildConfigField "String", "COVID_STATS_TOPIC", "\"covid-stats-android-dev\""
            buildConfigField "String", "STORAGE_BUCKET_ENDPOINT", STAGING_STORAGE_BUCKET_ENDPOINT
            buildConfigField "String", "GET_ACCESS_TOKEN_ENDPOINT", STAGING_GET_ACCESS_TOKEN_ENDPOINT
            buildConfigField "String", "UPLOAD_BUCKET_ENDPOINT", STAGING_UPLOAD_BUCKET_ENDPOINT
            buildConfigField "String", "COVID_INFO_TIMESTAMPS", STAGING_COVID_INFO_TIMESTAMPS
            buildConfigField "String", "COVID_INFO_DASHBOARD", STAGING_COVID_INFO_DASHBOARD
            buildConfigField "String", "COVID_INFO_DETAILS", STAGING_COVID_INFO_DETAILS
            buildConfigField "String", "COVID_INFO_VOIVODESHIPS", STAGING_COVID_INFO_VOIVODESHIPS
            buildConfigField "String", "COVID_TEST_URL", STAGING_COVID_TEST_URL
        }
        stage {
            dimension "mode"
            buildConfigField "String", "MAIN_TOPIC", "\"main-localized-stage\""
            buildConfigField "String", "DAILY_TOPIC", "\"daily-localized-stage\""
            buildConfigField "String", "COVID_STATS_TOPIC", "\"covid-stats-android-stage\""
            buildConfigField "String", "STORAGE_BUCKET_ENDPOINT", STAGING_STORAGE_BUCKET_ENDPOINT
            buildConfigField "String", "GET_ACCESS_TOKEN_ENDPOINT", STAGING_GET_ACCESS_TOKEN_ENDPOINT
            buildConfigField "String", "UPLOAD_BUCKET_ENDPOINT", STAGING_UPLOAD_BUCKET_ENDPOINT
            buildConfigField "String", "COVID_INFO_TIMESTAMPS", STAGING_COVID_INFO_TIMESTAMPS
            buildConfigField "String", "COVID_INFO_DASHBOARD", STAGING_COVID_INFO_DASHBOARD
            buildConfigField "String", "COVID_INFO_DETAILS", STAGING_COVID_INFO_DETAILS
            buildConfigField "String", "COVID_INFO_VOIVODESHIPS", STAGING_COVID_INFO_VOIVODESHIPS
            buildConfigField "String", "COVID_TEST_URL", STAGING_COVID_TEST_URL
        }
        prod {
            dimension "mode"
            buildConfigField "String", "MAIN_TOPIC", "\"main-localized\""
            buildConfigField "String", "DAILY_TOPIC", "\"daily-localized\""
            buildConfigField "String", "COVID_STATS_TOPIC", "\"covid-stats-android\""
            buildConfigField "String", "STORAGE_BUCKET_ENDPOINT", PRODUCTION_STORAGE_BUCKET_ENDPOINT
            buildConfigField "String", "GET_ACCESS_TOKEN_ENDPOINT", PRODUCTION_GET_ACCESS_TOKEN_ENDPOINT
            buildConfigField "String", "UPLOAD_BUCKET_ENDPOINT", PRODUCTION_UPLOAD_BUCKET_ENDPOINT
            buildConfigField "String", "COVID_INFO_TIMESTAMPS", PRODUCTION_COVID_INFO_TIMESTAMPS
            buildConfigField "String", "COVID_INFO_DASHBOARD", PRODUCTION_COVID_INFO_DASHBOARD
            buildConfigField "String", "COVID_INFO_DETAILS", PRODUCTION_COVID_INFO_DETAILS
            buildConfigField "String", "COVID_INFO_VOIVODESHIPS", PRODUCTION_COVID_INFO_VOIVODESHIPS
            buildConfigField "String", "COVID_TEST_URL", PRODUCTION_COVID_TEST_URL
        }
    }
}

// This handles the protocol buffer generation with wire
task generateWireClasses {
    def protoPath = 'src/main/proto'
    def wireGeneratedPath = 'src/main/java/'
    def wireGeneratedPathWithPackage = 'src/main/java/pl/gov/mc/protegosafe/data/exposuresproto'

    description = 'Generate Kotlin classes from protocol buffer (.proto) schema files for use with squareup\'s wire library'
    delete(wireGeneratedPathWithPackage)
    fileTree(dir: protoPath, include: '**/*.proto').each { File file ->
        doLast {
            javaexec {
                main = 'com.squareup.wire.WireCompiler'
                classpath = buildscript.configurations.classpath
                args = ["--proto_path=${protoPath}", "--kotlin_out=${wireGeneratedPath}", "${file}"]
            }
        }
    }
}

preBuild.dependsOn generateWireClasses

dependencies {
    implementation project(':exposurenotification')
    implementation deps.jetbrains.kotlin_stdlib
    implementation deps.androidx.appcompat_appcompat
    implementation deps.androidx.core_ktx
    implementation deps.androidx.security_crypto

    //ThreeTenABP
    implementation deps.time.threetenabp

    //Protobuf
    implementation deps.protobuf.wire_runtime

    //koin
    implementation deps.koin.android

    //rx
    implementation deps.rx.rxjava
    implementation deps.rx.rxkotlin
    implementation deps.rx.rxandroid

    //Timber
    implementation deps.timber.timber

    //Gson
    implementation deps.google.gson

    implementation deps.google.firebase_config_ktx

    //GMS
    implementation deps.google.play_services_base
    implementation deps.google.play_services_basement
    implementation deps.google.play_services_tasks

    //Guava
    implementation deps.google.guava

    //Facebook
    implementation deps.facebook.stetho_okhttp3

    // Retrofit
    implementation deps.retrofit.retrofit
    implementation deps.retrofit.adapter_rxjava2
    implementation deps.retrofit.converter_gson
    implementation deps.retrofit.logging_interceptor

    implementation deps.trustkit.trustkit
    // Supply androidx.preference.PreferenceManager required by TrustKit
    implementation deps.androidx.preference

    testImplementation deps.tests.junit
    androidTestImplementation deps.tests.junit_androidx_ext
    androidTestImplementation deps.tests.espresso

    implementation project(path: ":domain")
}
